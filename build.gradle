buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'fi.aalto'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.httpcomponents:httpclient:4.5.5'

    runtimeOnly 'com.h2database:h2'
    //brings junit4.12
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:3.1.1' //for rest-testing
    //spock dependencies
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.2'
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.2-groovy-2.5'
    testImplementation( 'com.athaydes:spock-reports:1.6.1' ) { //spock bdd-reports
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    testImplementation 'org.hamcrest:hamcrest-all:1.3' //hamcrest matchers for testing with rest-assured
    // optional dependencies for using Spock
    testRuntime  'net.bytebuddy:byte-buddy:1.8.21' // allows mocking of classes (in addition to interfaces)
    testRuntime 'org.objenesis:objenesis:2.6'   // allows mocking of classes without default constructor (together with CGLIB)
}

bootJar {
    baseName = 'testing-and-qa'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "org.codehaus.groovy") {
            details.useVersion "2.5.2"
        }
    }
}
